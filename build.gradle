project.ext.description = "Provides tool to use gorm in metridoc jobs and scripts"


buildscript {
    repositories {
        maven {
            url = "http://dl.bintray.com/upennlib/metridoc"
        }
    }

    dependencies {
        classpath "com.github.metridoc:metridoc-gradle-plugin:0.2.10"
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "http://jcenter.bintray.com/"
    }
    maven {
        url "http://dl.bintray.com/upennlib/metridoc"
    }
}

version = "0.4.2"
group = "com.github.metridoc"
archivesBaseName = "metridoc-tool-gorm"
sourceCompatibility = "1.6"
targetCompatibility = "1.6"
def grailsVersion = "2.2.3"
apply plugin: "groovy"
apply plugin: "metridoc"

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.0.8',
            'com.github.metridoc:metridoc-job-core:1.0.2',
            "org.grails:grails-core:$grailsVersion",
            "org.grails:grails-spring:$grailsVersion",
            "org.grails:grails-web:$grailsVersion",
            "org.grails:grails-plugin-datasource:$grailsVersion"

    compile("org.grails:grails-plugin-domain-class:$grailsVersion") {
        exclude module: "persistence-api"
    }

    compile("org.grails:grails-hibernate:$grailsVersion") {
        exclude module: "persistence-api"
    }

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0',
            'org.slf4j:slf4j-simple:1.7.5'
}


enableMaven { pom ->
    pom.project {
        name = archivesBaseName
        description = project.ext.description
        url = 'https://github.com/metridoc'
        organization {
            name = 'Upenn Libraries'
            url = 'https://github.com/metridoc'
        }
        licenses {
            license {
                name 'Educational Community License'
                //noinspection GroovyAssignabilityCheck
                url 'http://opensource.org/licenses/ECL-2.0'
                distribution 'repo'
            }
        }
        scm {
            url = 'https://github.com/metridoc/metridoc-tool-gorm'
            connection = 'https://github.com/metridoc/metridoc-tool-gorm'
            developerConnection = 'https://github.com/metridoc/metridoc-tool-gorm'
        }
        developers {
            developer {
                id = 'tbarker'
                name = 'Thomas Barker'
                email = 'tbarker@pobox.upenn.edu'
            }
        }
    }

    pom.whenConfigured { pomToConfigure ->
        pomToConfigure.dependencies.each { dep ->
            def classLoader = dep.getClass().classLoader
            def Exclusion = classLoader.loadClass("org.apache.maven.model.Exclusion")
            def exclusionToAdd = Exclusion.newInstance(
                    artifactId: "persistence-api",
                    groupId: "javax.persistence"
            )


            dep.exclusions.add(exclusionToAdd)
        }
    }
}
